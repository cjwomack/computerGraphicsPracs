#version 330 core

in vec2 fragmentTexCoord;

uniform sampler2D colorbuffer;
uniform float t;

out vec4 color;

const float offset = 1.0 / 640.0;
const vec2 offsets[9] = vec2[](
    vec2(-offset,  offset), // top-left
    vec2( 0.0f,    offset), // top-center
    vec2( offset,  offset), // top-right
    vec2(-offset,  0.0f),   // center-left
    vec2( 0.0f,    0.0f),   // center-center
    vec2( offset,  0.0f),   // center-right
    vec2(-offset, -offset), // bottom-left
    vec2( 0.0f,   -offset), // bottom-center
    vec2( offset, -offset)  // bottom-right    
);

const float sobel_dx_kernel[9] = float[](
    1, 0, -1,
    2, 0, -2,
    1, 0, -1
);

const float sobel_dy_kernel[9] = float[](
     1,  2,  1,
     0,  0,  0,
    -1, -2, -1
);

const float gauss_blur_kernel[9] = float[](
    1.0 / 16.0, 2.0 / 16.0, 1.0 / 16.0,
    2.0 / 16.0, 4.0 / 16.0, 2.0 / 16.0,
    1.0 / 16.0, 2.0 / 16.0, 1.0 / 16.0
);

vec4 Edge();
vec4 Blur();
float Luminosity_Grayscale(vec3 color);
vec2 Warp();

vec4 Edge() {

    vec2 texCoord = Warp();

    vec3 dx = vec3(0);
    vec3 dy = vec3(0);
    for(int i = 0; i < 9; i++)
    {
        dx += sobel_dx_kernel[i] * vec3(texture(colorbuffer, texCoord.st + offsets[i]));
        dy += sobel_dy_kernel[i] * vec3(texture(colorbuffer, texCoord.st + offsets[i]));
    }

    float r = sqrt(dx.r * dx.r + dy.r * dy.r);
    float g = sqrt(dx.g * dx.g + dy.g * dy.g);
    float b = sqrt(dx.b * dx.b + dy.b * dy.b);
    
    return vec4(r, g, b, 1.0);
}

vec4 Blur() {

    vec2 texCoord = Warp();

    vec3 color = vec3(0);

    for(int i = 0; i < 9; i++)
    {
        color += gauss_blur_kernel[i] * vec3(texture(colorbuffer, texCoord.st + offsets[i]));
    }
    
    return vec4(vec3(Luminosity_Grayscale(color)), 1.0);
}

float Luminosity_Grayscale(vec3 color) {

    vec3 coefficients = vec3(0.3, 0.59, 0.11);

    return dot(color, coefficients);
    
}

vec2 Warp() {
    const float amplitude = 0.01;
    return fragmentTexCoord + vec2(amplitude * sin(fragmentTexCoord.y * 20 + t), 0);
}

void main()
{
    color = 0.4 * vec4(240.0/255, 175.0/255, 129.0/255, 1.0) * Blur() + 0.6 * Edge();
}